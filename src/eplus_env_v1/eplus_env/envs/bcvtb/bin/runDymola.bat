@echo off
rem ============================================================
rem Script to run Dymola.
rem This script is called by the BCVTB to run Dymola on Windows.
rem
rem If dsin.txt and dymosim.exe exists, then 
rem dymosim.exe is started.
rem Otherwise, Dymola is started and the passed file executed.
rem 
rem mwetter@lbl.gov                                   2009-06-14
rem ============================================================
set argument=%1%

@echo Checking for Dymola
if exist "%BCVTB_DYMOLA_BIN%" goto GETEXT
@echo Error: Environment variable to Dymola is not valid.
@echo        BCVTB_DYMOLA_BIN=%BCVTB_DYMOLA_BIN%
@echo        Check bin/systemVariables-windows.properties
@echo        Exit with error.
goto END

:GETEXT
@echo Getting file extension
rem Get file extension
set ext=%argument:~-3%
if %ext%==mos goto CHECKFILES
@echo Error: First argument of %0 must be mos file.
@echo        Exit with error.
goto END

@echo Deleting temporary files generated by dymola
set fileSet=buildlog.txt dsfinal.txt dslog.txt dsmodel.c dsres.mat socket.cfg utilSocket.cfg
for %%a IN (%fileSet%) do call :saveDel %%a
goto NEXT 

rem Delete files if they exist
:saveDel
if exist %1 del %1
goto END

rem Continuation after the loop
:NEXT

:CHECKFILES
@echo Checking if dymosim.exe and dsin.txt exist
if not exist dymosim.exe goto RUNDYMOLA
if not exist dsin.txt    goto RUNDYMOLA

@echo Starting dymosim.exe
dymosim.exe -s
goto END

:RUNDYMOLA
@echo Copying files needed by Dymola
copy "%BCVTB_HOME%\lib\modelica\bcvtb.h" .
copy "%BCVTB_HOME%\lib\modelica\bcvtb_modelica.*" .
@echo Starting Dymola.exe
"%BCVTB_DYMOLA_BIN%" %argument%
goto END

:END
